#![allow(clippy)]
use std::str::FromStr;

use tokens;
use super::tree;
use self::tree::{
    TopDeclr,
    Expression,
    Expression_ as Expr,
    Import,
    ExportEntry,
    ExportEntry_,
    ExportList,
    ModuleDeclr,
    OperPriority,
    LetDeclaration,
    Pattern,
    Pattern_ as Pat,
    Type,
    Type_,
    TypeGenre_,
    Associativity,
    Anchored,
    Literal,
};

grammar;

Spanned<Rule>: Anchored<Rule, tokens::Location> = {
    <l: @L> <rule: Rule> <r: @R> => ((l,r),rule),
};

pub ModuleDeclr: Option<ModuleDeclr<String,tokens::Location>> = {
    NameTok "module" <name:NameTok>
    "where" "{" NameTok "=" NameTok ("," NameTok "=" NameTok)* "}"
    "exposing" <exports:ExportList> =>
        Some(ModuleDeclr { <> }),

    "port" "module" <name:NameTok> "exposing" <exports:ExportList> =>
        Some(ModuleDeclr { <> }),

    "module" <name:NameTok> "exposing" <exports:ExportList> =>
        Some(ModuleDeclr { <> }),
     =>
        None,
};

ExportList: ExportList<String,tokens::Location> = {
    "(" ".." ")" => ExportList::Unqualified,
    "(" <Comma<ExportEntry>> ")" => ExportList::List(<>),
};

ExportEntry: ExportEntry<String,tokens::Location> = Spanned<RawExportEntry>;
RawExportEntry: ExportEntry_<String> = {
    NameTok => ExportEntry_::Name(<>),
    "(" <OperatorTok> ")" => ExportEntry_::Operator(<>),
    <NameTok> "(" ".." ")" => ExportEntry_::WithAllConstructors(<>),
    <NameTok> "(" <Comma<NameTok>> ")" => ExportEntry_::WithConstructors(<>),

};

pub Import: Import<String,tokens::Location> = {
    <l:@L> "import" <global_name:NameTok>
    <local_name:("as" <NameTok>)?>
    <exposes:("exposing" <ExportList>)?> <r:@R> => Import {
        annot: (l,r),
        global_name,
        local_name,
        exposes,
    },
};


pub TopDeclr: TopDeclr<String,tokens::Location> = {
    <l:@L> "infixr" <precedence:NumberTok> <operator:OperatorTok> <r:@R> =>
        TopDeclr::OperPriority(OperPriority {
            associativity: Associativity::Right,
            precedence:
                u8::from_str(precedence.as_str())
                    .expect("Error reading precedence of infix operator"),
            operator: operator,
            location: (l,r),
        }),

    <l:@L> "infixl" <precedence:NumberTok> <operator:OperatorTok> <r:@R> =>
        TopDeclr::OperPriority(OperPriority {
            associativity: Associativity::Left,
            precedence:
                // The lexer can potentially generate Number tokens that
                // are not convertible to u8, we "guard" against that
                // (this should be temporary untill I figure out a
                // better error handling system).
                u8::from_str(precedence.as_str())
                    .expect("Error reading precedence of infix operator"),
            operator : operator,
            location: (l,r),
        }),

    <l:@L> "infix" <precedence:NumberTok> <operator:OperatorTok> <r:@R> =>
        TopDeclr::OperPriority(OperPriority {
            associativity: Associativity::Non,
            precedence:
                // The lexer can potentially generate Number tokens that
                // are not convertible to u8, we "guard" against that
                // (this should be temporary untill I figure out a
                // better error handling system).
                u8::from_str(precedence.as_str())
                    .expect("Error reading precedence of infix operator"),
            operator : operator,
            location: (l,r),
        }),

    <l:@L> "infix" <kind:NameTok> <precedence:NumberTok>
    "(" <name:OperatorTok> ")"
    "=" <function:NameTok> <r:@R> => {
        let associativity = match kind.as_ref() {
            "left" => Associativity::Left,
            "right" => Associativity::Right,
            "non" => Associativity::Non,
            _ => panic!("Invalid operator precedence qualification :("),
        };
        let priority = OperPriority {
            associativity, operator: name.clone(),
            precedence:
                // The lexer can potentially generate Number tokens that
                // are not convertible to u8, we "guard" against that
                // (this should be temporary untill I figure out a
                // better error handling system).
                u8::from_str(precedence.as_str())
                    .expect("Error reading precedence of infix operator"),
            location: (l,r),
        };
        TopDeclr::OperatorPrioDeclr { priority, function, name }
    },

    "type" <l:@L> <name:NameTok> <type_variables:NameTok*> "="
    <first_alt:Spanned<(<NameTok> <EnclosedType*>)>>
    <next_alts:("|"<Spanned<(<NameTok> <EnclosedType*>)>>)*> <r:@R> => {
        let mut alternatives = next_alts;
        alternatives.insert(0,first_alt);
        TopDeclr::Type((
            (l,r),
            TypeGenre_::Full { name, type_variables, alternatives },
        ))
    },

    "type" "alias" <l:@L> <name:NameTok> <type_variables:NameTok*>
    "=" <type_:Type> <r:@R> =>
        TopDeclr::Type((
            (l,r),
            TypeGenre_::Alias { name, type_variables, type_ }
        )),

    "(" <name:OperatorTok> ")" <arguments:EnclosedPattern*> "=" <body:Expression> =>
        TopDeclr::OperatorDeclr{<>},

    <p:"port"?> <name:NameTok> ":" <annotation:Type> =>
        TopDeclr::FunctionAnnotation {
            is_port:p.is_some(), name,  annotation
        },

    "(" <OperatorTok> ")" ":" <Type> =>
        TopDeclr::OperatorAnnotation(<>),

    <name:NameTok> <arguments:EnclosedPattern*> "=" <body:Expression> =>
        TopDeclr::FunctionDeclr{<>},

    <DocCommentTok> => TopDeclr::DocComment(<>),
};

Type: Type<String> = Spanned<RawType>;
RawType: Type_<String> = {
    <args:(<FunctionlessType> "->")+> <returns:FunctionlessType> => {
        let mut function_types = args;
        function_types.push(returns);
        Type_::Function(function_types)
    },

    RawFunctionlessType,
};

FunctionlessType: Type<String> = Spanned<RawFunctionlessType>;
RawFunctionlessType: Type_<String> = {
    NameTok EnclosedType+ => Type_::Application(<>),
    RawEnclosedType,
};

EnclosedType: Type<String> = Spanned<RawEnclosedType>;
RawEnclosedType: Type_<String> = {
    <name:NameTok> =>
        if name.chars().nth(0).map(|x| x.is_lowercase()) == Some(true) {
            Type_::Variable(name)
        } else {
            Type_::Terminal(name)
        },

    "(" <entries:(<Type> ",")+> <last:Type> ")" => {
        let mut entries = entries;
        entries.push(last);
        Type_::Tuple(entries)
    },

    "{" <variable_over:(<NameTok> "|")?>
    <fields:Comma<(<NameTok> ":" <Type>)>> "}" => {
        Type_::Record { variable_over, fields }
    },

    "{" "}" => Type_::EmptyRecord,
    "(" ")" => Type_::UnitType,
    "(" <RawType> ")",
};

Pattern: Pattern<String,tokens::Location> = Spanned<RawPattern>;
RawPattern: Pat<String,tokens::Location> = {
    <entries:(<AppliPattern> OperatorTok)+> <last:AppliPattern> => {
        let mut entries = entries;
        entries.push(last);
        Pat::Decons(entries)
    },

    RawAppliPattern,
};

AppliPattern: Pattern<String,tokens::Location> = Spanned<RawAppliPattern>;
RawAppliPattern: Pat<String,tokens::Location> = {
    <name:NameTok> <arguments:EnclosedPattern+> => {
        if name.chars().nth(0).map(|x| x.is_uppercase()) == Some(true) {
            Pat::ArgConstructor(name, arguments)
        } else {
            panic!("A bind located at the place of constructor")
        }
    },

    RawEnclosedPattern,
};

EnclosedPattern: Pattern<String,tokens::Location> = Spanned<RawEnclosedPattern>;
RawEnclosedPattern: Pat<String,tokens::Location> = {
    "{" <Comma<NameTok>> "}" => // TODO: bind only to lowercase
        Pat::Record(<>),

    "(" <p:Pattern> "as" <name:NameTok> ")" =>
        Pat::AliasBind(name, Box::new(p)),

    "(" <entries:(<Pattern> ",")+> <last:Pattern> ")" => {
        let mut entries = entries;
        entries.push(last);
        Pat::Tuple(entries)
    },

    <name:NameTok> =>
        if name.chars().nth(0).map(|x| x.is_lowercase()) == Some(true) {
            Pat::Bind(name)
        } else {
            Pat::Constructor(name)
        },

    "(" <RawPattern> ")",
    "(" ")" => Pat::UnitType,
    StringTok => Pat::StringLit(<>),
    NumberTok => Pat::Number(<>),
    CharTok => Pat::Character(<>),
    "_" => Pat::Discard,
    "[" "]" => Pat::EmptyList,
    "[" <Comma<Pattern>> "]" => Pat::List(<>),

};

Expression: Expression<String,tokens::Location> = Spanned<RawExpression>;
RawExpression: Expr<String,tokens::Location> = {
    <prefixes:(<ApplicationExpression> <OperatorTok>)+>
    <trailing:OpenExpression> =>
        Expr::InfixApplication{prefixes, trailing:Box::new(trailing)},

    RawOpenExpression,
};

CaseExpression: Expression<String,tokens::Location> = Spanned<RawCaseExpression>;
RawCaseExpression: Expr<String,tokens::Location> = {
    <prefixes:(<ApplicationExpression> <OperatorTok>)+>
    <trailing:CaseOpenExpression> =>
        Expr::InfixApplication{prefixes, trailing:Box::new(trailing)},

    RawCaseOpenExpression,
};

CaseOpenExpression: Expression<String,tokens::Location> = Spanned<RawCaseOpenExpression>;
RawCaseOpenExpression: Expr<String,tokens::Location> = {
    "case" <condition:Expression> "of"
    <init:(<Pattern> "->" <CaseExpression> "case indent")*>
    <last:(<Pattern> "->" <CaseExpression>)> "endcase" => {
        let mut branches = init;
        branches.push(last);
        Expr::CaseOf{condition:Box::new(condition), branches}
    },

    "lambda" <arguments:EnclosedPattern+> "->"
    <body:CaseExpression>  =>
        Expr::Lambda{arguments, body:Box::new(body)},

    "if" <ife:Expression>
    "then" <thene:Expression>
    "else" <elsee:CaseExpression> =>
        Expr::IfThenElse {
            condition: Box::new(ife),
            then_branch: Box::new(thene),
            else_branch: Box::new(elsee)
        },

    "let" <init:(<LetDeclaration> "let indent")*> <last:LetDeclaration>
    "in" <in_expr:CaseExpression> => {
        let mut declarations = init;
        declarations.push(last);
        Expr::LetIn {
            declarations,
            expression: Box::new(in_expr)
        }
    },

    RawApplicationExpression,
};

OpenExpression: Expression<String,tokens::Location> = Spanned<RawOpenExpression>;
RawOpenExpression: Expr<String,tokens::Location> = {
    "case" <condition:Expression> "of"
    <init:(<Pattern> "->" <CaseExpression> "case indent")*>
    <last:(<Pattern> "->" <CaseExpression>)> => {
        let mut branches = init;
        branches.push(last);
        Expr::CaseOf{condition:Box::new(condition), branches}
    },

    "lambda" <arguments:EnclosedPattern+> "->"
    <body:Expression>  =>
        Expr::Lambda{arguments, body:Box::new(body)},

    "if" <ife:Expression>
    "then" <thene:Expression>
    "else" <elsee:Expression> =>
        Expr::IfThenElse {
            condition: Box::new(ife),
            then_branch: Box::new(thene),
            else_branch: Box::new(elsee)
        },

    "let" <init:(<LetDeclaration> "let indent")*> <last:LetDeclaration>
    "in" <in_expr:Expression> => {
        let mut declarations = init;
        declarations.push(last);
        Expr::LetIn {
            declarations,
            expression: Box::new(in_expr)
        }
    },

    RawApplicationExpression,
};

ApplicationExpression: Expression<String,tokens::Location> = Spanned<RawApplicationExpression>;
RawApplicationExpression: Expr<String,tokens::Location> = {
    <head:ClosedExpression> <tail:ClosedExpression+> => {
        let mut expressions = tail;
        expressions.insert(0,head);
        Expr::Application(expressions)
    },

    RawClosedExpression,
};

ClosedExpression: Expression<String,tokens::Location> = Spanned<RawClosedExpression>;
RawClosedExpression: Expr<String,tokens::Location> = {
    "{" <updates:(<NameTok> "|")?>
    <fields:Comma<(<NameTok> "=" <Expression>)>> "}" =>
        Expr::Record{<>},

    "[" <Comma<Expression>> "]" =>
        Expr::List(<>),

    "(" <entries:(<Expression> ",")+> <last:Expression> ")" => {
        let mut entries = entries;
        entries.push(last);
        Expr::Tuple(entries)
    },

    "(" <commas:","+> ")" =>
        Expr::TupleConstructor((commas.len() + 1) as i16),

    "{" "}" => Expr::Record {
        updates: None,
        fields: Vec::new(),
    },
    "[" "]" => Expr::List(Vec::new()),
    "(" ")" => Expr::Tuple(Vec::new()),
    "(" <RawExpression> ")",
    StringTok => Expr::Literal(Literal::StringL, <>),
    NumberTok => Expr::Literal(Literal::Number,<>),
    CharTok => Expr::Literal(Literal::Char,<>),
    "(" <OperatorTok> ")" => Expr::PrefixOperator(<>),
    NameTok => Expr::Variable(<>),
};

LetDeclaration: LetDeclaration<String,tokens::Location> = {
    <annotation:(<NameTok> ":" <Type> "let indent")?>
    <name:NameTok> <arguments:EnclosedPattern+> "="
    <body:Expression> => {
        match annotation {
            Some((annot_name, maybe_annotation)) => {
                if annot_name != name {
                    // TODO: error handling
                    panic!("Type annotation name in let expression doesn't \
                           match the function declaration it is annotating")
                }
                let annotation = Some(maybe_annotation);
                LetDeclaration{
                    name: Some(name),
                    annotation, arguments, body
                }
            },
            None =>
                LetDeclaration{
                    name: Some(name),
                    annotation: None,
                    arguments, body
                },
        }
    },

    <annotation:(NameTok ":" <Type> "let indent")?>
    <pattern:EnclosedPattern> "=" <body:Expression> =>
        LetDeclaration{
            name: None,
            arguments: vec![pattern],
            annotation, body,
        },
};

Comma<T>: Vec<T> = {
    <v:(<T> ",")*> <e:T> => {
        let mut v = v;
        v.push(e);
        v
    }
};

extern {
    type Location = (u32,u16);
    type Error = tokens::LexError;

    enum tokens::ElmToken {
        "(" => tokens::ElmToken::LParens,
        ")" => tokens::ElmToken::RParens,
        "," => tokens::ElmToken::Comma,
        OperatorTok => tokens::ElmToken::Operator(<String>),
        ".." => tokens::ElmToken::Ellision,
        NameTok => tokens::ElmToken::Name(<String>),
        "module" => tokens::ElmToken::Module,
        "exposing" => tokens::ElmToken::Exposing,
        "import" => tokens::ElmToken::Import,
        "as" => tokens::ElmToken::As,
        "{" => tokens::ElmToken::LBrace,
        "}" => tokens::ElmToken::RBrace,
        DocCommentTok => tokens::ElmToken::DocComment(<String>),
        "[" => tokens::ElmToken::LBracket,
        "]" => tokens::ElmToken::RBracket,
        "lambda" => tokens::ElmToken::Lambda,
        "->" => tokens::ElmToken::RArrow,
        "case" => tokens::ElmToken::Case,
        "of" => tokens::ElmToken::Of,
        "_" => tokens::ElmToken::Underscore,
        "if" => tokens::ElmToken::If,
        "then" => tokens::ElmToken::Then,
        "else" => tokens::ElmToken::Else,
        "|" => tokens::ElmToken::Pipe,
        "=" => tokens::ElmToken::Assign,
        ":" => tokens::ElmToken::Colon,
        "type" => tokens::ElmToken::Type,
        "alias" => tokens::ElmToken::Alias,
        "infixr" => tokens::ElmToken::Infixr,
        "infixl" => tokens::ElmToken::Infixl,
        "infix" => tokens::ElmToken::Infix,
        "port" => tokens::ElmToken::Port,
        "where" => tokens::ElmToken::Where,
        "let" => tokens::ElmToken::Let,
        "in" => tokens::ElmToken::In,
        StringTok => tokens::ElmToken::StringLit(<String>),
        NumberTok => tokens::ElmToken::Number(<String>),
        CharTok => tokens::ElmToken::Char(<String>),
        "case indent" => tokens::ElmToken::CaseIndent,
        "let indent" => tokens::ElmToken::LetIndent,
        "endcase" => tokens::ElmToken::Endcase,
    }
}

// vim: ft=rust
